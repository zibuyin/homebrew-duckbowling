// Post processing shader that applies FXAA to the screen texture.
// Post processing order: Bloom -> HDR Tonemapping -> FXAA -> Gamma correction

#version 410 core

out vec4 FragColor;

in vec2 TexCoords;

// sample from screen framebuffer
uniform sampler2D screenTexture;

uniform int scrWidth;
uniform int scrHeight;

// assume uiTexture.wh = screenTexture.wh
uniform sampler2D uiTexture;
uniform int uiEnabled = 0;

#define GAMMA 2.2
#define EDGE_THRESHOLD_MIN 0.0312
#define EDGE_THRESHOLD_MAX 0.125
#define ITERATIONS 12
#define SUBPIXEL_QUALITY 0.75

const float steps[ITERATIONS] = float[ITERATIONS](1.0, 1.0, 1.0, 1.0, 1.0, 1.5, 2.0, 2.0, 2.0, 2.0, 4.0, 8.0);

float QUALITY(int i) { return steps[i]; }

float rgb2luma(vec3 rgb) { return sqrt(dot(rgb, vec3(0.299, 0.587, 0.114))); }

void main()
{
    // add ui framebuffer texture
    if (uiEnabled > 0)
    {
        vec3 uiSample = texture(uiTexture, TexCoords).rgb;
        const float UI_THRESHOLD = 0.01;
        if ((uiSample.r + uiSample.g + uiSample.b) > UI_THRESHOLD)
        {
            FragColor = vec4(uiSample, 1.0);
            return;
        }
    }

    vec2 invScreenSize = vec2(1.0 / float(scrWidth), 1.0 / float(scrHeight));
    vec3 colorCenter = texture(screenTexture, TexCoords).rgb;

    // get luma of current fragment
    float lumaCenter = rgb2luma(colorCenter);

    // get luma for neighbours
    float lumaDown = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(0, -1)).rgb);
    float lumaUp = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(0, 1)).rgb);
    float lumaLeft = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(-1, 0)).rgb);
    float lumaRight = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(1, 0)).rgb);

    // find the min and max luma around the current fragment
    float lumaMin = min(lumaCenter, min(min(lumaDown, lumaUp), min(lumaRight, lumaLeft)));
    float lumaMax = max(lumaCenter, max(max(lumaDown, lumaUp), max(lumaRight, lumaLeft)));

    // get the delta
    float lumaRange = lumaMax - lumaMin;

    // if luma variation isn't low enough or area is really dark there is no need for AA
    if (lumaRange < max(EDGE_THRESHOLD_MIN, lumaMax * EDGE_THRESHOLD_MAX))
    {
        vec3 mapped = pow(colorCenter, vec3(1.0 / GAMMA));
        FragColor = vec4(mapped, 1.0);
        return;
    }

    // get remaining 4 corners
    float lumaDownLeft = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(-1, -1)).rgb);
    float lumaUpRight = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(1, 1)).rgb);
    float lumaUpLeft = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(-1, 1)).rgb);
    float lumaDownRight = rgb2luma(textureOffset(screenTexture, TexCoords, ivec2(1, -1)).rgb);

    // combine four edge lumas
    float lumaDownUp = lumaDown + lumaUp;
    float lumaLeftRight = lumaLeft + lumaRight;

    // same for corners
    float lumaLeftCorners = lumaDownLeft + lumaUpLeft;
    float lumaDownCorners = lumaDownLeft + lumaDownRight;
    float lumaRightCorners = lumaDownRight + lumaUpRight;
    float lumaUpCorners = lumaUpRight + lumaUpLeft;

    // estimation for gradient along horizontal and vertical axis
    float edgeHorizontal = abs(-2.0 * lumaLeft + lumaLeftCorners) + abs(-2.0 * lumaCenter + lumaDownUp) * 2.0 +
        abs(-2.0 * lumaRight + lumaRightCorners);
    float edgeVertical = abs(-2.0 * lumaUp + lumaUpCorners) + abs(-2.0 * lumaCenter + lumaLeftRight) * 2.0 +
        abs(-2.0 * lumaDown + lumaDownCorners);

    bool isHorizontal = (edgeHorizontal >= edgeVertical);

    // get edge orientation
    float luma1 = isHorizontal ? lumaDown : lumaLeft;
    float luma2 = isHorizontal ? lumaUp : lumaRight;

    // get gradients
    float gradient1 = luma1 - lumaCenter;
    float gradient2 = luma2 - lumaCenter;

    // get steepest direction
    bool is1Steepest = abs(gradient1) >= abs(gradient2);

    float gradientScaled = 0.25 * max(abs(gradient1), abs(gradient2));

    // get step size
    float stepLength = isHorizontal ? invScreenSize.y : invScreenSize.x;

    // average the luma in correct direction
    float lumaLocalAverage = 0.0;
    if (is1Steepest)
    {
        stepLength = -stepLength;
        lumaLocalAverage = 0.5 * (luma1 + lumaCenter);
    }
    else
    {
        lumaLocalAverage = 0.5 * (luma2 + lumaCenter);
    }

    // shift UV
    vec2 currentUV = TexCoords;
    if (isHorizontal)
    {
        currentUV.y += stepLength * 0.5;
    }
    else
    {
        currentUV.x += stepLength * 0.5;
    }

    // first iteration
    vec2 offset = isHorizontal ? vec2(invScreenSize.x, 0.0) : vec2(0.0, invScreenSize.y);
    vec2 uv1 = currentUV - offset;
    vec2 uv2 = currentUV + offset; // search opposite direction

    float lumaEnd1 = rgb2luma(texture(screenTexture, uv1).rgb);
    float lumaEnd2 = rgb2luma(texture(screenTexture, uv2).rgb);
    lumaEnd1 -= lumaLocalAverage;
    lumaEnd2 -= lumaLocalAverage;

    bool reached1 = abs(lumaEnd1) >= gradientScaled;
    bool reached2 = abs(lumaEnd2) >= gradientScaled;
    bool reachedBoth = reached1 && reached2;

    if (!reached1)
    {
        uv1 -= offset;
    }

    if (!reached2)
    {
        uv2 += offset;
    }

    if (!reachedBoth)
    {
        for (int i = 2; i < ITERATIONS; ++i)
        {
            if (!reached1)
            {
                lumaEnd1 = rgb2luma(texture(screenTexture, uv1).rgb);
                lumaEnd1 = lumaEnd1 - lumaLocalAverage;
            }

            if (!reached2)
            {
                lumaEnd2 = rgb2luma(texture(screenTexture, uv2).rgb);
                lumaEnd2 = lumaEnd2 - lumaLocalAverage;
            }

            reached1 = abs(lumaEnd1) >= gradientScaled;
            reached2 = abs(lumaEnd2) >= gradientScaled;
            reachedBoth = reached1 && reached2;

            if (!reached1)
            {
                uv1 -= offset * QUALITY(i);
            }

            if (!reached2)
            {
                uv2 += offset * QUALITY(i);
            }

            if (reachedBoth)
            {
                break;
            }
        }
    }

    float distance1 = isHorizontal ? (TexCoords.x - uv1.x) : (TexCoords.y - uv1.y);
    float distance2 = isHorizontal ? (uv2.x - TexCoords.x) : (uv2.y - TexCoords.y);

    bool isDirection1 = distance1 < distance2;
    float distanceFinal = min(distance1, distance2);

    float edgeThickness = distance1 + distance2;

    float pixelOffset = -distanceFinal / edgeThickness + 0.5;

    bool isLumaCenterSmaller = lumaCenter < lumaLocalAverage;
    bool correctVariation = ((isDirection1 ? lumaEnd1 : lumaEnd2) < 0.0) != isLumaCenterSmaller;

    float finalOffset = correctVariation ? pixelOffset : 0.0;

    // sub pixel antialiasing
    float lumaAverage =
        (1.0 / float(ITERATIONS)) * (2.0 * (lumaDownUp + lumaLeftRight) + lumaLeftCorners + lumaRightCorners);

    float subPixelOffset1 = clamp(abs(lumaAverage - lumaCenter) / lumaRange, 0.0, 1.0);
    float subPixelOffset2 = (-2.0 * subPixelOffset1 + 3.0) * subPixelOffset1 * subPixelOffset1;

    float subPixelOffsetFinal = subPixelOffset1 * subPixelOffset2 * SUBPIXEL_QUALITY;

    finalOffset = max(finalOffset, subPixelOffsetFinal);

    // final read
    vec2 finalUV = TexCoords;
    if (isHorizontal)
    {
        finalUV.y += finalOffset * stepLength;
    }
    else
    {
        finalUV.x += finalOffset * stepLength;
    }

    vec3 mapped = texture(screenTexture, finalUV).rgb;
    mapped = pow(mapped, vec3(1.0 / GAMMA));

    FragColor = vec4(mapped, 1.0);
}
